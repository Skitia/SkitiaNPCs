IF 
Dead("X3KJIL")
Global("X3KaleQuestJillianDied","LOCALS",0)
THEN RESPONSE #100
SetGlobal("X3KaleQuestJillianDied","LOCALS",1)
AddJournalEntry(@30006,QUEST_DONE) // Quest is over because Jillian died.
//Though the player can still continue as far as they are able to if they are on the Chester route.
END 

IF 
OR(3)
Global("X3HZavatarQuest","GLOBAL",2)
Global("X3HZavatarQuest","GLOBAL",1)
Global("X3KalQuest","GLOBAL",9)
THEN RESPONSE #100
StartDialogueNoSet(Player1)
END

IF 
See("X3KMERC")
!Dead("X3KMERC")
THEN RESPONSE #100 
Attack("X3KMERC")
END 

IF 
See("X3KMERC2")
Dead("X3KMERC")
!Dead("X3KMERC2")
THEN RESPONSE #100 
Attack("X3KMERC2")
END 

IF 
See("X3KMERC3")
Dead("X3KMERC")
Dead("X3KMERC2")
!Dead("X3KMERC3")
THEN RESPONSE #100 
Attack("X3KMERC3")
END 

IF 
Global("X3KalQuest","GLOBAL",6)
Dead("X3KMERC3")
Dead("X3KMERC2")
Dead("X3KMERC")
THEN RESPONSE #100 
Wait(1)
StartDialogueNoSet(Player1)
END

  IF
    AttackedBy([GOODCUTOFF],DEFAULT)
    Allegiance(Myself,NEUTRAL)
	OR(2)
	InParty("X3Hel")
	InParty("X3Kal")
  THEN
    RESPONSE #100
      Enemy()
	  ActionOverride("X3Hel",ChangeAIScript("",DEFAULT))
	  ActionOverride("X3Hel",ChangeAIScript("",GENERAL))
	  ActionOverride("X3Hel",LeaveParty())
	  ActionOverride("X3Hel",Enemy())
	  ActionOverride("X3Kal",ChangeAIScript("",DEFAULT))
	  ActionOverride("X3Kal",ChangeAIScript("",GENERAL))
	  ActionOverride("X3Kal",LeaveParty())
	  ActionOverride("X3Kal",Enemy())	  
 END 
