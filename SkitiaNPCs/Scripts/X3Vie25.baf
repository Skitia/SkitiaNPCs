// Talk immediately when summoned. 
IF
Global("X3VieSummoned","GLOBAL",1)
THEN
RESPONSE #100
Wait(1)
StartDialogueNoSet([PC])
END


//Talk Initialization 
IF 
InParty(Myself)
Global("X3VieToBTalk","LOCALS",0)
GlobalGT("X3VieApp","GLOBAL",0)
!Global("X3VieRomanceActive","GLOBAL",2)
!AreaCheck("AR4000")
!AreaCheck("AR4500")
THEN RESPONSE #100 
RealSetGlobalTimer("X3VieToBTalkTimer","GLOBAL",3200)
SetGlobal("X3VieToBTalk","LOCALS",1)
END 

//Love Talk Initialization: These can fire earlier.
IF 
InParty(Myself)
Global("X3VieToBLoveTalk","LOCALS",0)
Global("X3VieRomanceActive","GLOBAL",2)
THEN RESPONSE #100 
RealSetGlobalTimer("X3VieToBLoveTimer","GLOBAL",3200)
SetGlobal("X3VieToBLoveTalk","LOCALS",1)
END 

//Friendship Talks 
IF 
	RealGlobalTimerExpired("X3VieToBTalkTimer","GLOBAL")
	CombatCounter(0)
    !See([ENEMY])
	!AreaType(DUNGEON)
	GlobalGT("X3VieApp","GLOBAL",0)
	OR(3)
	 Global("X3VieToBTalk","LOCALS",1)
	 Global("X3VieToBTalk","LOCALS",3)
	 Global("X3VieToBTalk","LOCALS",5)
THEN
  RESPONSE #100
	IncrementGlobal("X3VieToBTalk","LOCALS",1)
END

IF 
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
    OR(3)
    Global("X3VieToBTalk","LOCALS",2)
	Global("X3VieToBTalk","LOCALS",4)
	Global("X3VieToBTalk","LOCALS",6)
THEN RESPONSE #100 
	RealSetGlobalTimer("X3VieToBTalkTimer","GLOBAL",3200)
	StartDialogueNoSet(Player1)
END 


//Love Talks 
IF
	RealGlobalTimerExpired("X3VieToBLoveTimer","GLOBAL")
	Global("X3VieRomanceActive","GLOBAL",2) 
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
	CombatCounter(0)
    !See([ENEMY])
	!AreaType(DUNGEON)
	OR(2)
	Global("X3VieToBLoveTalk","LOCALS",1)
	Global("X3VieToBLoveTalk","LOCALS",9)
THEN RESPONSE #100 
	IncrementGlobal("X3VieToBLoveTalk","LOCALS",1)
END
//Post Rest Talk 
IF 
	PartyRested()
	RealGlobalTimerExpired("X3VieToBLoveTimer","GLOBAL")
	Global("X3VieRomanceActive","GLOBAL",2) 
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
	CombatCounter(0)
    !See([ENEMY])
	!AreaType(DUNGEON)
	Global("X3VieToBLoveTalk","LOCALS",3)
THEN RESPONSE #100 
	IncrementGlobal("X3VieToBLoveTalk","LOCALS",1)
END
// Saradush Sacked Post Talk 
IF 
	RealGlobalTimerExpired("X3VieToBLoveTimer","GLOBAL")
	Global("X3VieRomanceActive","GLOBAL",2) 
	Global("SaradushSacked","GLOBAL",1)
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
	CombatCounter(0)
    !See([ENEMY])
	!AreaType(DUNGEON)
	Global("X3VieToBLoveTalk","LOCALS",5)
THEN RESPONSE #100 
	IncrementGlobal("X3VieToBLoveTalk","LOCALS",1)
END
// Sage talked to: Catches player up in case some content was lost before.  
IF 
	RealGlobalTimerExpired("X3VieToBLoveTimer","GLOBAL")
	Global("X3VieRomanceActive","GLOBAL",2) 
	!Global("X3VEvermeetEnd","GLOBAL",0)
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
	CombatCounter(0)
    !See([ENEMY])
	!AreaType(DUNGEON)
	GlobalLT("X3VieToBLoveTalk","LOCALS",8)
THEN RESPONSE #100 
	SetGlobal("X3VieToBLoveTalk","LOCALS",8)
END
// Sendai Killed Post Talk 
IF 
	Dead("Sendai")
	RealGlobalTimerExpired("X3VieToBLoveTimer","GLOBAL")
	Global("X3VieRomanceActive","GLOBAL",2) 
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
	CombatCounter(0)
    !See([ENEMY])
	!AreaType(DUNGEON)
	Global("X3VieToBLoveTalk","LOCALS",11)
THEN RESPONSE #100 
	IncrementGlobal("X3VieToBLoveTalk","LOCALS",1)
END
// Challenges Done Post Talk 
IF 
	Global("ChallengesDone","GLOBAL",1)
	RealGlobalTimerExpired("X3VieToBLoveTimer","GLOBAL")
	Global("X3VieRomanceActive","GLOBAL",2) 
	Global("ChallengesDone","GLOBAL",1)
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
	CombatCounter(0)
    !See([ENEMY])
	Global("X3VieToBLoveTalk","LOCALS",13)
THEN RESPONSE #100 
	IncrementGlobal("X3VieToBLoveTalk","LOCALS",1)
END

//Wraith Talk 
IF 
	Global("X3LovedOne","GLOBAL",4)
	Global("X3VieRomanceActive","GLOBAL",2)	
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	Global("X3VieWraithTalk","LOCALS",0)	
THEN RESPONSE #100 
	IncrementGlobal("X3VieWraithTalk","LOCALS",1)
END 

IF 
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
	Global("X3VieRomanceActive","GLOBAL",2) 
    OR(7)
    Global("X3VieToBLoveTalk","LOCALS",2)
	Global("X3VieToBLoveTalk","LOCALS",4)
	Global("X3VieToBLoveTalk","LOCALS",6)
	Global("X3VieToBLoveTalk","LOCALS",10)
	Global("X3VieToBLoveTalk","LOCALS",12)
	Global("X3VieToBLoveTalk","LOCALS",14)
	Global("X3VieWraithTalk","LOCALS",1)
THEN RESPONSE #100 
	PlaySong(0)
	RealSetGlobalTimer("X3VieLoveTimer","GLOBAL",3200)
	StartDialogueNoSet(Player1)
END 

IF 
InParty(Myself)
Switch("X3VieAppChange","GLOBAL")
THEN RESPONSE #1
SetGlobal("X3VieAppChange","GLOBAL",0)
IncrementGlobal("X3VieApp","GLOBAL",-10)
DisplayStringNoNameDlg(Player1,@509)
RESPONSE #2
SetGlobal("X3VieAppChange","GLOBAL",0)
IncrementGlobal("X3VieApp","GLOBAL",-8)
DisplayStringNoNameDlg(Player1,@509)
RESPONSE #3
SetGlobal("X3VieAppChange","GLOBAL",0)
IncrementGlobal("X3VieApp","GLOBAL",-6)
DisplayStringNoNameDlg(Player1,@506)
RESPONSE #4
SetGlobal("X3VieAppChange","GLOBAL",0)
IncrementGlobal("X3VieApp","GLOBAL",-4)
DisplayStringNoNameDlg(Player1,@506)
RESPONSE #5
SetGlobal("X3VieAppChange","GLOBAL",0)
IncrementGlobal("X3VieApp","GLOBAL",-2)
DisplayStringNoNameDlg(Player1,@503)
RESPONSE #6
SetGlobal("X3VieAppChange","GLOBAL",0)
RESPONSE #7
SetGlobal("X3VieAppChange","GLOBAL",0)
IncrementGlobal("X3VieApp","GLOBAL",2)
DisplayStringNoNameDlg(Player1,@513)
RESPONSE #8
SetGlobal("X3VieAppChange","GLOBAL",0)
IncrementGlobal("X3VieApp","GLOBAL",4)
DisplayStringNoNameDlg(Player1,@516)
RESPONSE #9
SetGlobal("X3VieAppChange","GLOBAL",0)
IncrementGlobal("X3VieApp","GLOBAL",6)
DisplayStringNoNameDlg(Player1,@516)
RESPONSE #10
SetGlobal("X3VieAppChange","GLOBAL",0)
IncrementGlobal("X3VieApp","GLOBAL",8)
DisplayStringNoNameDlg(Player1,@519)
RESPONSE #11
SetGlobal("X3VieAppChange","GLOBAL",0)
IncrementGlobal("X3VieApp","GLOBAL",10)
DisplayStringNoNameDlg(Player1,@519)
END 

// If not in party, it should be reset with no approval changes. 
IF 
!InParty(Myself) 
!Global("X3VieAppChange","GLOBAL",0)
THEN RESPONSE #100 
SetGlobal("X3VieAppChange","GLOBAL",0)
END


//Reset to 100 if past max/reward bonus stats.
IF 
GlobalGT("X3VieApp","GLOBAL",100)
InParty(Myself)
Switch("X3VieAppMaxBonus","GLOBAL")
THEN RESPONSE #0
SetGlobal("X3VieApp","GLOBAL",100)
SetGlobal("X3VieAppMaxBonus","GLOBAL",1)
ChangeStat(Myself,DEX,1,ADD)
ChangeStat(Myself,STR,1,ADD)
DisplayStringNoNameDlg(Player1,@99)
RESPONSE #1 
SetGlobal("X3VieApp","GLOBAL",100)
END 

IF 
GlobalLT("X3VieApp","GLOBAL",-40)
InParty(Myself)
Switch("X3Leave","LOCALS")
THEN RESPONSE #0
SetGlobal("X3Break","LOCALS",1)
StartDialogueNoSet(Player1)
RESPONSE #2
SetGlobal("X3Break","LOCALS",3)
StartDialogueNoSet(Player1) // Leaves for Good
END 



//Engagement Ring 
IF 
HasItemEquiped("X3ERing",Myself)
Global("X3Engagement","LOCALS",0)
IsValidForPartyDialogue(Myself)
IsValidForPartyDialogue(Player1)
CombatCounter(0)
!See([ENEMY])
THEN RESPONSE #100 
IncrementGlobal("X3Engagement","LOCALS",1)
StartDialogueNoSet(Player1)
END  

IF 
HasItemEquiped("X3ERing",Myself)
Global("X3Engagement","LOCALS",2)
IsValidForPartyDialogue(Myself)
IsValidForPartyDialogue(Player1)
CombatCounter(0)
!See([ENEMY])
THEN RESPONSE #100 
DisplayString("X3Vie",~You could beg and I'd still not take that ring.~)
ActionOverride(Player1,TakePartyItem("X3ERing"))
END 

//Approval Low/In Romance: Breakup Conversation
IF 
Global("X3VieRomanceActive","GLOBAL",2)
GlobalLT("X3VieApp","GLOBAL",50)
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
Global("X3BreakUp","LOCALS",0)
THEN RESPONSE #100 
SetGlobalTimer("X3BreakUp","GLOBAL",THREE_DAYS)
SetGlobal("X3BreakUp","LOCALS",1)
END 

IF 
GlobalTimerExpired("X3BreakUp","GLOBAL")
Global("X3VieRomanceActive","GLOBAL",2)
GlobalLT("X3VieApp","GLOBAL",50)
See(Player1)
IsValidForPartyDialogue(Myself)
IsValidForPartyDialogue(Player1)
CombatCounter(0)
!AreaType(DUNGEON)
!See([ENEMY])
Global("X3BreakUp","LOCALS",1)
THEN RESPONSE #100 
SetGlobal("X3BreakUp","LOCALS",2)
StartDialogueNoSet(Player1)
END 

IF 
Global("X3BreakUp","LOCALS",1)
GlobalTimerExpired("X3BreakUp","GLOBAL")
GlobalGT("X3VieApp","GLOBAL",49)
InParty("X3Vie")
CombatCounter(0)
!See([ENEMY])
THEN RESPONSE #100 
SetGlobal("X3BreakUp","LOCALS",0)
END 
