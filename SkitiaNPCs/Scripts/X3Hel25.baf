// Talk immediately when summoned. 
IF
Global("X3HelSummoned","GLOBAL",1)
THEN
RESPONSE #100
Wait(1)
StartDialogueNoSet([PC])
END

//Talk Initialization 
IF 
InParty(Myself)
Global("X3HelToBTalk","LOCALS",0)
GlobalGT("X3HelApp","GLOBAL",0)
THEN RESPONSE #100 
RealSetGlobalTimer("X3HelToBTalkTimer","GLOBAL",3600)
SetGlobal("X3HelToBTalk","LOCALS",1)
END 



IF 
Global("SaradushSacked","GLOBAL",1)
Global("X3HelToBQuest","GLOBAL",0)
THEN RESPONSE #100 
RealSetGlobalTimer("X3HelgaQuestTimerToB","GLOBAL",3600)
SetGlobal("X3HelToBQuest","GLOBAL",1)
END 

// Friendship Talk #1
IF 
	Global("X3HelToBTalk","LOCALS",1)
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    RealGlobalTimerExpired("X3HelToBTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
	!AreaType(DUNGEON)
	GlobalGT("X3HelApp","GLOBAL",0)
THEN RESPONSE #100	
	IncrementGlobal("X3HelToBTalk","LOCALS",1)
END 
// Friendship Talk #2
IF 
	Dead("Yaga01")
	Global("X3HelToBTalk","LOCALS",3)
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    RealGlobalTimerExpired("X3HelToBTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
	!AreaType(DUNGEON)
	GlobalGT("X3HelApp","GLOBAL",0)
THEN RESPONSE #100	
	RealSetGlobalTimer("X3HelToBTalkTimer","GLOBAL",30)
	IncrementGlobal("X3HelToBTalk","LOCALS",1)
END 
// Friendship Talk #3
IF 
	Dead("balth")
	Global("X3HelToBTalk","LOCALS",5)
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    RealGlobalTimerExpired("X3HelToBTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
	!AreaType(DUNGEON)
	GlobalGT("X3HelApp","GLOBAL",0)
THEN RESPONSE #100	
	RealSetGlobalTimer("X3HelToBTalkTimer","GLOBAL",30)
	IncrementGlobal("X3HelToBTalk","LOCALS",1)
END 
//Fire ToB Friendship Talks 
IF 
	RealGlobalTimerExpired("X3HelToBTalkTimer","GLOBAL")
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
    OR(3)
    Global("X3HelToBTalk","LOCALS",2)
	Global("X3HelToBTalk","LOCALS",4)
    Global("X3HelToBTalk","LOCALS",6)
THEN RESPONSE #100 
	RealSetGlobalTimer("X3HelToBTalkTimer","GLOBAL",3600)
	StartDialogueNoSet(Player1)
END 
// Quest Talk 	
IF 
PartyRested()
RealGlobalTimerExpired("X3HelgaQuestTimerToB","GLOBAL")
See(Player1)
!See([ENEMY])
!Range([NEUTRAL],10)
CombatCounter(0)
Global("X3HelToBQuest","GLOBAL",1)
THEN RESPONSE #100 
IncrementGlobal("X3HelToBQuest","GLOBAL",1)
StartDialogueNoSet([PC])
END 

//Engagement Ring - Always Rejected
IF 
HasItemEquiped("X3ERing",Myself)
Global("X3Engagement","LOCALS",0)
IsValidForPartyDialogue(Myself)
IsValidForPartyDialogue(Player1)
CombatCounter(0)
!See([ENEMY])
THEN RESPONSE #100 
IncrementGlobal("X3Engagement","LOCALS",1)
StartDialogueNoSet(Player1)
END  

IF 
HasItemEquiped("X3ERing",Myself)
Global("X3Engagement","LOCALS",2)
IsValidForPartyDialogue(Myself)
IsValidForPartyDialogue(Player1)
CombatCounter(0)
!See([ENEMY])
THEN RESPONSE #100 
DisplayString("X3Hel",~Grr! Yer sense of humor is nay amusing, <CHARNAME>.~)
ActionOverride(Player1,TakePartyItem("X3ERing"))
END

// Approval Changes 
IF 
InParty(Myself)
Switch("X3HelAppChange","GLOBAL")
THEN RESPONSE #1
SetGlobal("X3HelAppChange","GLOBAL",0)
IncrementGlobal("X3HelApp","GLOBAL",-10)
DisplayStringNoNameDlg(Player1,@209)
RESPONSE #2
SetGlobal("X3HelAppChange","GLOBAL",0)
IncrementGlobal("X3HelApp","GLOBAL",-8)
DisplayStringNoNameDlg(Player1,@209)
RESPONSE #3
SetGlobal("X3HelAppChange","GLOBAL",0)
IncrementGlobal("X3HelApp","GLOBAL",-6)
DisplayStringNoNameDlg(Player1,@206)
RESPONSE #4
SetGlobal("X3HelAppChange","GLOBAL",0)
IncrementGlobal("X3HelApp","GLOBAL",-4)
DisplayStringNoNameDlg(Player1,@206)
RESPONSE #5
SetGlobal("X3HelAppChange","GLOBAL",0)
IncrementGlobal("X3HelApp","GLOBAL",-2)
DisplayStringNoNameDlg(Player1,@203)
RESPONSE #6
SetGlobal("X3HelAppChange","GLOBAL",0)
RESPONSE #7
SetGlobal("X3HelAppChange","GLOBAL",0)
IncrementGlobal("X3HelApp","GLOBAL",2)
DisplayStringNoNameDlg(Player1,@213)
RESPONSE #8
SetGlobal("X3HelAppChange","GLOBAL",0)
IncrementGlobal("X3HelApp","GLOBAL",4)
DisplayStringNoNameDlg(Player1,@216)
RESPONSE #9
SetGlobal("X3HelAppChange","GLOBAL",0)
IncrementGlobal("X3HelApp","GLOBAL",6)
DisplayStringNoNameDlg(Player1,@216)
RESPONSE #10
SetGlobal("X3HelAppChange","GLOBAL",0)
IncrementGlobal("X3HelApp","GLOBAL",8)
DisplayStringNoNameDlg(Player1,@219)
RESPONSE #11
SetGlobal("X3HelAppChange","GLOBAL",0)
IncrementGlobal("X3HelApp","GLOBAL",10)
DisplayStringNoNameDlg(Player1,@219)
END 

// If not in party, it should be reset with no approval changes. 
IF 
!InParty(Myself) 
!Global("X3HelAppChange","GLOBAL",0)
THEN RESPONSE #100 
SetGlobal("X3HelAppChange","GLOBAL",0)
END

//Reset to 90 if past max/reward bonus stats.
IF 
GlobalGT("X3HelApp","GLOBAL",90)
InParty(Myself)
Switch("X3HelAppMaxBonus","GLOBAL")
THEN RESPONSE #0
SetGlobal("X3HelApp","GLOBAL",90)
SetGlobal("X3HelAppMaxBonus","GLOBAL",1)
ChangeStat(Myself,WIS,1,ADD)
ChangeStat(Myself,CON,1,ADD)
DisplayStringNoNameDlg(Player1,@99)
RESPONSE #1 
SetGlobal("X3HelApp","GLOBAL",90)
END 


IF 
GlobalLT("X3HelApp","GLOBAL",-30)
InParty(Myself)
Switch("X3Leave","LOCALS")
THEN RESPONSE #0
SetGlobal("X3Break","LOCALS",1)
StartDialogueNoSet(Player1)
RESPONSE #2
SetGlobal("X3Break","LOCALS",3)
StartDialogueNoSet(Player1) // Leaves for Good
END 

//Reputation Changes 
IF
ReputationLT(Player1,6)
Global("X3BadRepAppLoss","LOCALS",0)
InParty(Myself)
THEN RESPONSE #100
SetGlobal("X3BadRepAppLoss","LOCALS",1)
IncrementGlobal("X3HelApp","GLOBAL",-3)
DisplayStringNoNameDlg(Player1,@203)
END
IF
ReputationLT(Player1,4)
Global("X3BadRepAppLoss","LOCALS",1)
InParty(Myself)
THEN RESPONSE #100
SetGlobal("X3BadRepAppLoss","LOCALS",2)
IncrementGlobal("X3HelApp","GLOBAL",-6)
DisplayStringNoNameDlg(Player1,@206)
END
IF
ReputationLT(Player1,2)
Global("X3BadRepAppLoss","LOCALS",2)
InParty(Myself)
THEN RESPONSE #100
SetGlobal("X3BadRepAppLoss","LOCALS",3)
IncrementGlobal("X3HelApp","GLOBAL",-9)
DisplayStringNoNameDlg(Player1,@209)
END