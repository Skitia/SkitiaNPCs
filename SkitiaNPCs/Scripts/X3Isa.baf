//Friendship Bonus 
IF 
InParty(Myself) 
Global("X3IsaPartyBG1","GLOBAL",1)
Global("X3IsaAppBonus","LOCALS",0)
THEN RESPONSE #100
SetGlobal("X3IsaAppBonus","LOCALS",1)
IncrementGlobal("X3IsaApp","GLOBAL",9)
END 


//Talk Initialization 
IF 
InParty(Myself)
Global("X3IsaTalk","LOCALS",0)
THEN RESPONSE #100 
RealSetGlobalTimer("X3IsaTalkTimer","GLOBAL",3100)
SetGlobal("X3IsaTalk","LOCALS",1)
END 

// Romance Check 
IF 
//Requirements 
Global("X3IsaInterest","LOCALS",1) // Interest Shown
GlobalGT("X3IsaApp","GLOBAL",29)
GlobalGT("X3IsaTalk","LOCALS",10)
Global("X3IsaRomanceActive","GLOBAL",0)
InParty(Myself) 
Gender(Player1,FEMALE)
//No Other Romance Alive
!Global("AerieRomanceActive","GLOBAL",2) // And no one else active. 
!Global("JaheiraRomanceActive","GLOBAL",2)
!Global("ViconiaRomanceActive","GLOBAL",2)
!Global("AnomenRomanceActive","GLOBAL",2)
!Global("RasaadRomanceActive","GLOBAL",2)
!Global("NeeraRomanceActive","GLOBAL",2)
!Global("X3IsaRomanceActive","GLOBAL",2) // In case she is recruited later.
!Global("X3KalRomanceActive","GLOBAL",2)
!Global("X3VieRomanceActive","GLOBAL",2)
!Global("DornRomanceActive","GLOBAL",2)
!Global("HexxatRomanceActive","GLOBAL",2)
!Global("L3PetsyRomanceActive","GLOBAL",2)
!Global("YvetteRomanceActive","GLOBAL",2)
!Global("C0SireneRomanceActive","GLOBAL",2)
THEN RESPONSE #100 
RealSetGlobalTimer("X3IsaLoveTimer","GLOBAL",3400)
SetGlobal("X3IsaRomanceActive","GLOBAL",1)
SetGlobal("X3IsaLoveTalk","LOCALS",1)
END 

//Friendship Talks 
IF
    IsValidForPartyDialogue(Myself)
	IsValidForPartyDialogue(Player1)
    See(Player1)
    RealGlobalTimerExpired("X3IsaTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
	!AreaType(DUNGEON)
	GlobalGT("X3IsaApp","GLOBAL",0)
    OR(5)
     Global("X3IsaTalk","LOCALS",1)
	 Global("X3IsaTalk","LOCALS",3)
     Global("X3IsaTalk","LOCALS",5)
	 Global("X3IsaTalk","LOCALS",7)
	 Global("X3IsaTalk","LOCALS",9)
THEN
  RESPONSE #100
	IncrementGlobal("X3IsaTalk","LOCALS",1)
END



IF 
InParty("X3Isa")
Global("X3IsaQuestReminder","LOCALS",0)
THEN RESPONSE #100
SetGlobalTimer("X3IsaReminder","GLOBAL",SEVEN_DAYS)
SetGlobal("X3IsaQuestReminder","LOCALS",1)
END 

IF 
GlobalTimerExpired("X3IsaReminder","GLOBAL")
IsValidForPartyDialogue("X3Isa")
IsValidForPartyDialogue(Player1)
See(Player1)
CombatCounter(0)
!See([ENEMY])
Global("X3IsaQuest","GLOBAL",0)
OR(3)
Global("X3IsaQuestReminder","LOCALS",1)
Global("X3IsaQuestReminder","LOCALS",3)
Global("X3IsaQuestReminder","LOCALS",5)
THEN RESPONSE #100
IncrementGlobal("X3IsaQuestReminder","LOCALS",1)
END 

IF 
IsValidForPartyDialogue("X3Isa")
IsValidForPartyDialogue(Player1)
See(Player1)
CombatCounter(0)
!See([ENEMY])
OR(9)
Global("X3IsaTalk","LOCALS",2)
Global("X3IsaTalk","LOCALS",4)
Global("X3IsaTalk","LOCALS",6)
Global("X3IsaTalk","LOCALS",8)
Global("X3IsaTalk","LOCALS",10)
Global("X3IsaTalk","LOCALS",12)
Global("X3IsaQuestReminder","LOCALS",2)
Global("X3IsaQuestReminder","LOCALS",4)
Global("X3IsaQuestReminder","LOCALS",6)
THEN RESPONSE #100
StartDialogueNoSet(Player1)
END 

IF 
!Dead("X3IMavis")
Global("X3IsaQuest","GLOBAL",4)
!See("X3IMavis")
See(Player1)
CombatCounter(0)
!See([ENEMY])
IsValidForPartyDialogue("X3Isa")
IsValidForPartyDialogue(Player1)
Global("X3IsaQuestTalk1","LOCALS",0)
THEN RESPONSE #100
SetGlobal("X3IsaQuestTalk1","LOCALS",1)
END 

IF 
Dead("X3IMavis")
OR(2)
Global("X3IsaQuest","GLOBAL",4)
Global("X3IMavisEnemy","GLOBAL",1)
!AreaCheck("AR0411")
See(Player1)
CombatCounter(0)
!See([ENEMY])
IsValidForPartyDialogue("X3Isa")
IsValidForPartyDialogue(Player1)
Global("X3IsaQuestTalk2","LOCALS",0)
THEN RESPONSE #100
SetGlobal("X3IsaQuestTalk2","LOCALS",1)
END 

IF 
Dead("X3IPHL")
Global("X3IPlanarHunterEnemy","GLOBAL",1)
!AreaCheck("AR0411")
See(Player1)
CombatCounter(0)
!See([ENEMY])
IsValidForPartyDialogue("X3Isa")
IsValidForPartyDialogue(Player1)
Global("X3IsaQuestTalk2","LOCALS",0)
THEN RESPONSE #100
SetGlobal("X3IsaQuestTalk2","LOCALS",1)
END 


IF 
OR(2)
Global("X3IsaQuestTalk1","LOCALS",1)
Global("X3IsaQuestTalk2","LOCALS",1)
See(Player1)
CombatCounter(0)
!See([ENEMY])
IsValidForPartyDialogue("X3Isa")
IsValidForPartyDialogue(Player1)
THEN RESPONSE #100
StartDialogueNoSet(Player1)
END 

IF 
Global("PCSphere","GLOBAL",2)
!Dead("X3IMavis")
GlobalGT("X3IsaQuest","GLOBAL",0)
GlobalLT("X3IsaQuest","GLOBAL",6)
THEN RESPONSE #100
SetGlobal("X3IsaQuest","GLOBAL",-1)
AddJournalEntry(@6017,QUEST)
END 


//App Shifts 
IF 
InParty(Myself)
Switch("X3IsaAppChange","GLOBAL")
THEN RESPONSE #1
SetGlobal("X3IsaAppChange","GLOBAL",0)
IncrementGlobal("X3IsaApp","GLOBAL",-10)
DisplayStringNoNameDlg(Player1,@600609)
RESPONSE #2
SetGlobal("X3IsaAppChange","GLOBAL",0)
IncrementGlobal("X3IsaApp","GLOBAL",-8)
DisplayStringNoNameDlg(Player1,@600609)
RESPONSE #3
SetGlobal("X3IsaAppChange","GLOBAL",0)
IncrementGlobal("X3IsaApp","GLOBAL",-6)
DisplayStringNoNameDlg(Player1,@600606)
RESPONSE #4
SetGlobal("X3IsaAppChange","GLOBAL",0)
IncrementGlobal("X3IsaApp","GLOBAL",-4)
DisplayStringNoNameDlg(Player1,@600606)
RESPONSE #5
SetGlobal("X3IsaAppChange","GLOBAL",0)
IncrementGlobal("X3IsaApp","GLOBAL",-2)
DisplayStringNoNameDlg(Player1,@600603)
RESPONSE #6
SetGlobal("X3IsaAppChange","GLOBAL",0)
RESPONSE #7
SetGlobal("X3IsaAppChange","GLOBAL",0)
IncrementGlobal("X3IsaApp","GLOBAL",2)
DisplayStringNoNameDlg(Player1,@600613)
RESPONSE #8
SetGlobal("X3IsaAppChange","GLOBAL",0)
IncrementGlobal("X3IsaApp","GLOBAL",4)
DisplayStringNoNameDlg(Player1,@600616)
RESPONSE #9
SetGlobal("X3IsaAppChange","GLOBAL",0)
IncrementGlobal("X3IsaApp","GLOBAL",6)
DisplayStringNoNameDlg(Player1,@600616)
RESPONSE #10
SetGlobal("X3IsaAppChange","GLOBAL",0)
IncrementGlobal("X3IsaApp","GLOBAL",8)
DisplayStringNoNameDlg(Player1,@600619)
RESPONSE #11
SetGlobal("X3IsaAppChange","GLOBAL",0)
IncrementGlobal("X3IsaApp","GLOBAL",10)
DisplayStringNoNameDlg(Player1,@600619)
END 


// If not in party, it should be reset with no approval changes. 
IF 
!InParty(Myself) 
!Global("X3IsaAppChange","GLOBAL",0)
THEN RESPONSE #100 
SetGlobal("X3IsaAppChange","GLOBAL",0)
END

//Reset to 100 if past max/reward bonus stats.
IF 
GlobalGT("X3IsaApp","GLOBAL",100)
InParty(Myself)
Switch("X3IsaAppMaxBonus","GLOBAL")
THEN RESPONSE #0
SetGlobal("X3IsaApp","GLOBAL",100)
SetGlobal("X3IsaAppMaxBonus","GLOBAL",1)
ChangeStat(Myself,STR,1,ADD)
ChangeStat(Myself,INT,1,ADD)
DisplayStringNoNameDlg(Player1,@60099)
RESPONSE #1 
SetGlobal("X3IsaApp","GLOBAL",100)
END 


IF 
GlobalLT("X3IsaApp","GLOBAL",-40)
InParty(Myself)
Switch("X3Leave","LOCALS")
THEN RESPONSE #0
SetGlobal("X3Break","LOCALS",1)
StartDialogueNoSet(Player1)
RESPONSE #2
SetGlobal("X3Break","LOCALS",3)
StartDialogueNoSet(Player1) // Leaves for Good
END 

//Reputation Changes 
IF
ReputationGT(Player1,16)
Global("X3BadRepAppLoss","LOCALS",0)
InParty(Myself)
Alignment(Myself,LAWFUL_EVIL)
THEN RESPONSE #100
SetGlobal("X3BadRepAppLoss","LOCALS",1)
IncrementGlobal("X3IsaApp","GLOBAL",-3)
DisplayStringNoNameDlg(Player1,@600603)
END
IF
ReputationGT(Player1,19)
Alignment(Myself,LAWFUL_EVIL)
Global("X3BadRepAppLoss","LOCALS",1)
InParty(Myself)
THEN RESPONSE #100
SetGlobal("X3BadRepAppLoss","LOCALS",2)
IncrementGlobal("X3IsaApp","GLOBAL",-6)
DisplayStringNoNameDlg(Player1,@600606)
END


//Approval Low/In Romance: Breakup Conversation
IF 
Global("X3IsaRomanceActive","GLOBAL",2)
GlobalLT("X3IsaApp","GLOBAL",50)
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
Global("X3BreakUp","LOCALS",0)
THEN RESPONSE #100 
SetGlobalTimer("X3BreakUp","GLOBAL",THREE_DAYS)
SetGlobal("X3BreakUp","LOCALS",1)
END 

IF 
GlobalTimerExpired("X3BreakUp","GLOBAL")
Global("X3IsaRomanceActive","GLOBAL",2)
GlobalLT("X3IsaApp","GLOBAL",50)
See(Player1)
IsValidForPartyDialogue(Myself)
IsValidForPartyDialogue(Player1)
CombatCounter(0)
!AreaType(DUNGEON)
!See([ENEMY])
Global("X3BreakUp","LOCALS",1)
THEN RESPONSE #100 
SetGlobal("X3BreakUp","LOCALS",2)
StartDialogueNoSet(Player1)
END 

IF 
Global("X3BreakUp","LOCALS",1)
GlobalTimerExpired("X3BreakUp","GLOBAL")
GlobalGT("X3IsaApp","GLOBAL",49)
InParty("X3Isa")
CombatCounter(0)
!See([ENEMY])
THEN RESPONSE #100 
SetGlobal("X3BreakUp","LOCALS",0)
END 

// If Isaac becomes a vampire, here's his post-vampire dialogue:
IF
Global("X3IsaVampire","GLOBAL",4)
See(Player1)
IsValidForPartyDialogue(Player1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END


// Flirts
IF
InParty(Myself)
Global("X3IsaFlirtsStart","LOCALS",0)
OR(2)
Global("X3IsaRomanceActive","GLOBAL",2)
Global("X3IsaRomanceActive","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("X3IsaFlirtsStart","LOCALS",1)
RealSetGlobalTimer("X3IsaFlirtTimer","GLOBAL",300)
END

IF
InParty(Myself)
RealGlobalTimerExpired("X3NaliaFlirtTimer","GLOBAL")
Global("X3IsaFlirtsStart","LOCALS",0)
Global("X3DisableFlirts","LOCALS",0)
IsValidForPartyDialogue(Myself)
IsValidForPartyDialogue(Player1)
OR(3)
Global("X3IsaRomanceActive","GLOBAL",2)
Global("X3IsaRomanceActive","GLOBAL",1)
See(Player1)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("X3IsaStartFlirt","GLOBAL",1)
END

IF
InParty(Myself)
Global("X3IsaStartFlirt","GLOBAL",1)
IsValidForPartyDialogue(Myself)
IsValidForPartyDialogue(Player1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

IF
InParty(Myself)
RealGlobalTimerExpired("X3IsaFlirtTimer","GLOBAL")
Global("X3DisableFlirts","LOCALS",0)
OR(2)
Global("X3IsaRomanceActive","GLOBAL",2)
Global("X3IsaRomanceActive","GLOBAL",1)
!CombatCounter(0)
THEN
RESPONSE #100
RealSetGlobalTimer("X3IsaFlirtTimer","GLOBAL",600)
END

//Experience 

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,1249999)
	XPLT(Myself,1250000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,1250000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,999999)
	XPLT(Myself,1000000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,1000000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,749999)
	XPLT(Myself,750000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,750000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,499999)
	XPLT(Myself,500000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,500000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,249999)
	XPLT(Myself,250000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,250000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,124999)
	XPLT(Myself,125000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,125000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPLT(Player1,89001)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		SetInterrupt(TRUE)
END

